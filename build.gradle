plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.23.3'
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()
    jcenter()
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "opmod" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

spotless {
    java {
        googleJavaFormat()
        target 'src/**/*.java'
    }
}

tasks.named('build') {
    dependsOn 'spotlessCheck'
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation include('net.kyori:adventure-platform-fabric:6.3.0')
    modImplementation 'net.kyori:adventure-api:4.17.0'
    modImplementation include("io.github.cdagaming:DiscordIPC:0.10.6")
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    inputs.property "archivesName", project.base.archivesName
    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    repositories {

    }
}